; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Anomaly Editor"
#define MyAppVersion GetFileVersion("..\Release\Anomaly.exe")
#define MyAppPublisher "Anomalous Software"
#define MyAppURL "http://www.anomalousmedical.com"
#define MyAppExeName "Anomaly.exe"

#if Exec('S:\DRM\CodeKey\SignEditor.bat') != 0
#error Could not sign
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{DA0C94F1-997E-4315-95B4-E9B8E63E2531}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\Anomalous Software\Anomaly
DefaultGroupName=Anomalous Software
LicenseFile=..\..\Medical\AnomalousMedical\Installer\License\en.rtf
OutputDir=..\Release\Setups
OutputBaseFilename=AnomalySetup
Compression=lzma
SolidCompression=yes
SignTool=AnomalousMedicalSign $qAnomaly Editor$q $f
ChangesAssociations=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: ..\Release\Anomaly.exe; DestDir: {app}; Flags: ignoreversion 
Source: ..\Release\ImageAtlasPacker.exe; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\OgreModelEditor.exe; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\BEPUikPlugin.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\BulletPlugin.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\DotNetZip.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\Engine.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\FreeImageNET.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\GuiFramework.Cameras.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\GuiFramework.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\GuiFramework.Editor.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\libRocketPlugin.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\libRocketWidget.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\MyGUIPlugin.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\OgrePlugin.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\OSPlatform.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\ShapeLoader.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\Release\SoundPlugin.dll; DestDir: {app}; Flags: ignoreversion

;x86
Source: ..\Release\x86\BulletWrapper.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\d3dcompiler_47.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\FreeImage.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\libRocketWrapper.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\MyGUIWrapper.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\OgreCWrapper.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\OgreMain.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\OpenAL32.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\OSHelper.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\RenderSystem_Direct3D11.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\RenderSystem_GL.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\SoundWrapper.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\WinMTDriver.dll; DestDir: {app}\x86; Flags: ignoreversion
Source: ..\Release\x86\Zip.dll; DestDir: {app}\x86; Flags: ignoreversion

;x64
Source: ..\Release\x64\BulletWrapper.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\d3dcompiler_47.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\FreeImage.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\libRocketWrapper.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\MyGUIWrapper.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\OgreCWrapper.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\OgreMain.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\OpenAL32.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\OSHelper.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\RenderSystem_Direct3D11.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\RenderSystem_GL.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\SoundWrapper.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\WinMTDriver.dll; DestDir: {app}\x64; Flags: ignoreversion
Source: ..\Release\x64\Zip.dll; DestDir: {app}\x64; Flags: ignoreversion

;Microcode Caches
;Source: ..\..\Medical\Release\Direct3D11 Rendering Subsystem.mcc; DestDir: {app}; Flags: ignoreversion
;Source: ..\..\Medical\Release\OpenGL Rendering Subsystem.mcc; DestDir: {app}; Flags: ignoreversion

;VS 2013 Redistributable
Source: "..\..\dependencies\InstallerDependencies\Windows\vcredist_x86.exe"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall
Source: "..\..\dependencies\InstallerDependencies\Windows\vcredist_x64.exe"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall

;.Net 4.5.1
Source: ..\..\dependencies\InstallerDependencies\Windows\NDP451-KB2859818-Web.exe; DestDir: {tmp}; 

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\Ogre Model Editor"; Filename: "{app}\OgreModelEditor.exe"
Name: "{group}\Image Atlas Packer"; Filename: "{app}\ImageAtlasPacker.exe"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\vcredist_x86.exe"; Parameters: "/q /norestart"; StatusMsg: "Installing Visual Studio 2013 Redistributable (x86)";
Filename: "{tmp}\vcredist_x64.exe"; Parameters: "/q /norestart"; StatusMsg: "Installing Visual Studio 2013 Redistributable (x64)";
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, "&", "&&")}}"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCR; Subkey: ".mesh"; ValueType: string; ValueName: ""; ValueData: "OgreMeshFiles"; Flags: uninsdeletevalue 
Root: HKCR; Subkey: "OgreMeshFiles"; ValueType: string; ValueName: ""; ValueData: "Ogre Mesh File"; Flags: uninsdeletekey 
Root: HKCR; Subkey: "OgreMeshFiles\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\OgreModelEditor.exe,0" 
Root: HKCR; Subkey: "OgreMeshFiles\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\OgreModelEditor.exe"" ""%1""" 

[Code]
procedure checkdotnetfx4();
var
  resultCode: Integer;
	release: cardinal;
begin
	RegQueryDWordValue(HKLM, 'Software\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', release);
	if (release < 378759) and (release <> 378675) and (release <> 378758) then 
	begin
    if MsgBox('You need to install the Microsoft .Net Framework 4.5.1.'#13#10'If you are connected to the internet you can do this now.'#13#10'Would you like to continue?', mbConfirmation, MB_YESNO) = IDYES then
      begin
        Exec(ExpandConstant('{tmp}\NDP451-KB2859818-Web.exe'), '/norestart', '', SW_SHOW, ewWaitUntilTerminated, resultCode);
      end
    else
      begin
        MsgBox('You must install the Microsoft .Net Framework 4.5.1 for this program to work.'#13#10'Please visit www.anomalousmedical.com for more info.', mbInformation, MB_OK)
      end;
   end;
//	   if resultCode=3010 then
	   //Restart required
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if(CurStep = ssPostInstall) then
    checkdotnetfx4();
end;
